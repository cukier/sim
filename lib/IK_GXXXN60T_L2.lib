*****************************************************************
* Level2 Version of 600V IGBT3 Models oct 2007			*
*****************************************************************
* INFINEON TECHNOLOGIES AG      	IKXXXN60T_L2.LIB	*
*								*
*  (V2.6c)     	10/07  	(updated)				*
*                                                               *
* Models provided by INFINEON are not warranted by INFINEON as  *
* fully representing all of the specifications and operating    *
* characteristics of the semiconductor product to which the     *
* model relates. The model describe the characteristics of a    * 
* typical device.                                               * 
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. INFINEON therefore does not  *
* assume any liability arising from their use.                  *
* INFINEON reserves the right to change models without prior    * 
* notice.                                                       *
*                                                               *
* This library contains Level 2 models for the following	* 
* INFINEON Technologies 600 V IGBT3				*
* devices/chip-models including lead inductances related	*
*								*
* to packages:							*
*								*
* type				BV/V	Ice/A			*	
*								*
* DUOPACKS							*
* IKX04N60T			600	4 (comp. all 4A devs)	*
* IKX06N60T			600	6 (comp. all 6A devs)	*
* IKX10N60T			600	10 (comp. all 10A devs)	*
* IKX15N60T			600	15 (comp. all 15A devs)	*
* IKX20N60T			600	20 (comp. all 20A devs)	*
* IKX30N60T			600	30 (comp. all 30A devs)	*
* IKX50N60T			600	50 (comp. all 50A devs)	*
* IKX75N60T			600	75 (comp. all 75A devs)	*
*								*
* SINGLE IGBTS							*
* IGX06N60T			600	6 (comp. all 6A devs)	*
* IGX10N60T			600	10 (comp. all 10A devs)	*
* IGX15N60T			600	15 (comp. all 15A devs)	*
* IGX30N60T			600	30 (comp. all 30A devs)	*
* IGX50N60T			600	50 (comp. all 50A devs)	*
* IGX75N60T			600	75 (comp. all 75A devs)	*
*								*
*								*
* the model is based on the publication:			*
* 								*
* R.Kraus, P.Türkes, J.Sigg					*
* Physics-based Models Of Power Semiconductor Devices 		*
* For The Circuit Simulator Spice				*
* 								*
* Power Electronics Specialists Conference, 1998.		* 
* PESC 98 Record. 29th Annual IEEE				*
*								*
* SUPPORTemail:	simulate@infineon.com				*
*****************************************************************
.SUBCKT IKX04N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS:   TJ = {TJ} Atotal = 0.031	A = 0.014  Rg = 10m
XDIO	katl	anol		L4XXXM_L2	PARAMS:   TJ = {TJ} A=0.0077	N_ideal = 1.0
.ENDS
*$
*****************************************************************
.SUBCKT IKX06N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS:   TJ = {TJ} Atotal = 0.041	A = 0.022  Rg = 10m
XDIO	katl	anol		L4XXXM_L2	PARAMS:   TJ = {TJ} A=0.0131	N_ideal = 1.0
.ENDS
*$
*****************************************************************
.SUBCKT IKX10N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.059	A = 0.036  Rg = 10m
XDIO	katl	anol		L4XXXM_L2	PARAMS: TJ = {TJ} A=0.021	N_ideal = 1.0
.ENDS
*$
*****************************************************************
.SUBCKT IKX15N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.08	A = 0.052  Rg = 10m
XDIO	katl	anol		L4XXXM_L2	PARAMS: TJ = {TJ} A=0.0288	N_ideal = 1.0
.ENDS
*$
*****************************************************************
.SUBCKT IKX20N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.102	A = 0.071  Rg = 10m
XDIO	katl	anol		L4XXXM_L2	PARAMS: TJ = {TJ} A=0.0386	N_ideal = 1.0
.ENDS
*$
*****************************************************************
.SUBCKT IKX30N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.152	A = 0.107  Rg = 10m
XDIO	katl	anol		L4XXXM_L2	PARAMS: TJ = {TJ} A=0.0577	N_ideal = 1.0
.ENDS
*$
*****************************************************************
.SUBCKT IKX50N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.276	A = 0.20  Rg = 10m
XDIO	katl	anol		L4XXXM_L2	PARAMS: TJ = {TJ} A=0.1112	N_ideal = 1.0
.ENDS
*$
*****************************************************************
.SUBCKT IKX75N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.389	A = 0.30  Rg = 10m
XDIO	katl	anol		L4XXXM_L2	PARAMS: TJ = {TJ} A=0.1666	N_ideal = 1.0
.ENDS
*$
*****************************************************************
.SUBCKT IGX06N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS:   TJ = {TJ} Atotal = 0.041	A = 0.022  Rg = 10m
.ENDS
*$
*****************************************************************
.SUBCKT IGX10N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.059	A = 0.036  Rg = 10m
.ENDS
*$
*****************************************************************
.SUBCKT IGX15N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.08	A = 0.052  Rg = 10m
.ENDS
*$
*****************************************************************
.SUBCKT IGX20N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.102	A = 0.071  Rg = 10m
.ENDS
*$
*****************************************************************
.SUBCKT IGX30N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.152	A = 0.107  Rg = 10m
.ENDS
*$
*****************************************************************
.SUBCKT IGX50N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.276	A = 0.20  Rg = 10m
.ENDS
*$
*****************************************************************
.SUBCKT IGX75N60T_L2 ano gate kat PARAMS:	TJ = 27
* 
LANO	ano	anol	5n
RANO	ano	anol	10
LKAT	katl	kat	7n
RKAT	katl	kat	10
XL75xxD anol	gate	katl	L75xxD_L2 	PARAMS: TJ = {TJ}  Atotal = 0.389	A = 0.30  Rg = 10m
.ENDS
*$
*****************************************************************
.SUBCKT L75xxD_L2 ano gate kat PARAMS: TJ = 27 Atotal = 1.5 A = 1.2 Rg = 1
.PARAM 
+q = 1.6E-19	eps0 = 8.85E-14	epsi = 11.8	eox= 2.8  
+t0= 273	k=1.381e-23	
+un = 1350      up = 450	u_surf= 650	ni0=1.45e10
+vlimit = 8e6 pi = 3.1416  vsat = 8e5
.PARAM
+TX1 = 110e-7
+NA2 = 1.2e17 NA3 = 7.5E13 NA1=1e15
+TX2 = 3u	ETAUB = 2       wb0 = 45E-4 EMU = -2
+BV_FW=650	BV_RW=28  
.PARAM wb = 65E-4
.PARAM  Lspec =2.401e-4
.PARAM
+lchann=2.0e-4	
+vsw = -.5
.PARAM
+Rs = 0.2625m	
.PARAM nzell = 		{A*5.569e5}
.PARAM Ads =		{nzell*0.5184u}
.PARAM wchann=		{nzell*28.8u}
.PARAM Cox_d = 		{nzell*2.076e-14}
.PARAM Cox1 = 		{nzell*8.330e-14}
.PARAM Cox2 =		0
.PARAM Cox_g = 		{(Atotal-A)*22.527n}
.PARAM Cox_fp =		{(Cox1 + Cox2 + Cox_g)}
.PARAM Cgs = 		{nzell*1.298e-14}
.PARAM Cje =		{Atotal *2.994n}
.PARAM Agd_d  =		{Cox_d*4.439e7}
.PARAM Agd_fp  =	{A - Ads + nzell*1.851u}
.PARAM A_j =		{nzell*1.796u}
.PARAM Ise0 =		{A*48.96p}
.PARAM Isbs2=		{A*52.26p} 
.PARAM Isbs2_g=		{A*3.333e-14} 
.PARAM Isbs1=		{A*7.127u} 
.PARAM Isbs1_g=		{A*2.136e-19} 
.PARAM  Q0 =		{A*54.07n}
MFET    dx	g	s	s	MOS W={wchann},L={lchann/100}
GMFET_T	dn	kat	VALUE = {LIMIT(I(VIMOS)*((TJ+t0)/300)**{EMU},-1e6,1e6) }
GDE1	ano	e1	VALUE = 
+ {TANH(1e3*I(VDE1))*(((Isx1(TJ,V(xj1,0))**((t0 + TJ)/(t0 + 27)))/Isx1(27,V(xj1,0)))**((t0 + 27)/(t0 + TJ)) 
+ * ABS(I(VDE1))**((t0 + 27)/(t0 + TJ)) - ABS(I(VDE1)))}
GDE2	ano	e1	VALUE = 
+ {TANH(1e3*I(VDE2))*(((Isx2(TJ)**((t0 + TJ)/(t0 + 27)))/Isx2(27))**((t0 + 27)/(t0 + TJ)) 
+ * ABS(I(VDE2))**((t0 + 27)/(t0 + TJ)) - ABS(I(VDE2)))}
CPN	ano_x	e1	{Cje}
RJE	ano	ano_x	1m
GJCAP	d	kat	VALUE = {1e6*I(VIJCT)}
VDE1	ano	anx1	0
VDE2	ano	anx2	0
DE1	anx1	e1	D11
DE2	anx2	e1	D12
DS      kat	d	D2  
.MODEL  MOS     NMOS    (LEVEL=3,nsub={NA2},tox={TX1/100},uo={u_surf},cgso=1p,cgdo=1p,THETA = 0.05)
.MODEL	D11	D	(IS = {Isbs1}, N = 2)
.MODEL	D12	D	(IS = {Isbs2}, N = 1.0)
.MODEL  D2      D       (IS={ISE0},BV={BV_FW})
RG1	gate	g_t	{(Rg + .25)/(((TJ + t0)/300)**EMU)}
RS	s	kat	{Rs/A}
ETHERM  g_t	g	VALUE = {DVt(TJ)}
EVGCOX	vgcox	0	VALUE { V(ox_fp,kat)-{vsw} }
GICOX_fp  g	ox_fp	VALUE = {((Cox_fp)/100 + 99*Cox_fp*(1+ tanh(100*V(vgcox,0)) )/200)* 1e6*I(VDUGD_fp)}
GICOX_d  g	ox_d	VALUE = {((Cox_d)/100 + 99*Cox_d*(1+ tanh(100*V(vgcox,0)) )/200)* 1e6*I(VDUGD_d)}
GICGS	g	s	VALUE = {(Cgs + 99*(Cox_fp + Cox_d)*(1 + tanh(-100*V(vgcox,0)) )/200)* 1e6*I(VDUGS)}
VIA	e1	e	0
EDEP_fp	d	ox_fp	VALUE = {MAX(Vdep_fp(V(d,g),V(xj1,0),I(VIPC)),0)}
EDEP_d	d	ox_d	VALUE = {MAX(Vdep_d(V(d,g),V(xj1,0),I(VIPC)),0)}
EGIA	e	d	VALUE = {I(VIA)*Rb(V(b,0),TJ)}
VINC	d	dn	0
EAUX1	dxvr 	kat	VALUE = {V(d,kat)}
ERDRAIN	dxvr 	dxv 	VALUE = {I(VIMOS)*RDR(V(nsqr),V(dxvr,dxv),TJ)}
VIMOS	dxv	dx	0
GIC	ds	s	VALUE = {IPC(I(VINC),V(b,0),V(xj1,0),I(VDXJ),TJ)}
VIPC	d	ds	0
CQB	b 0	1u
RHELP	b 0	1meg
GRQB	b 0	VALUE   {1u*V(b,0)*(1/(TAU_X(TJ,V(xj1,0)) )) }
GINC	0 b	VALUE	{I(VINC)}
GINE	b 0	VALUE	{INE(I(VINC),V(b,0),V(xj1,0),I(VDXJ),TJ)}
EXJ	xj1 0	VALUE	{MAX(xj(V(d,kat),I(VIPC),MAX(V(xj1,0),1e-4)), 1e-4)}
CXJ	xj  0	1u
RXJ	xjr  xj 0.1m
VDXJ	xj1 xjr	0
EDUGS	du1	0	VALUE {V(g,s)}
VDUGS	du1	du1cr	0
RDUGS	du1cr	du1c	.1m	
CDUGS	du1c	0	1u
EDUGD_d	du2	0	VALUE {V(g,ox_d)}
VDUGD_d	du2	du2cr	0
RDUGD_d	du2cr	du2c	.1m	
CDUGD_d	du2c	0	1u
EDUGD_fp	du3	0	VALUE {V(g,ox_fp)}
VDUGD_fp	du3	du3cr	0
RDUGD_fp	du3cr	du3c	.1m	
CDUGD_fp	du3c	0	1u
EJUNCT	jct 0	VALUE = {QTOT(V(xj1,0),I(VIPC))}
VIJCT	jct cpr	0
RIJCT	cpr cp	.1m
CJCT	0   cp	1u
ENSQR   nsqr	0	VALUE = {MAX(NSQR(I(VINC),V(dx,s),TJ),0)}
.FUNC	NSQR(I,V,T)	{MAX(I,0)/MAX(V,0.001)*lchann/wchann/(u_surf*((T+t0)/300)**EMU)/q/3.2e-4/1e16}
.FUNC	RDR(X,V,T)	{3.2e-4/Ads/q/(MAX(NA3,1e16*X))
+ /(un*((T + t0)/300)**EMU)*(1+((un*((T + t0)/300)**EMU)*MAX(V,0)/3.2e-4)/vsat)}
.FUNC  N1(X,Y)		{(2*(NA3 + MAX(Y,0)/q/A/vlimit  )+ NA1)*pi*X}
.FUNC  N2(X)		{NA1*Lspec*log(1+((X-wb0)/Lspec)**2)}
.FUNC  N3(X)		{2*NA1*arctan((-X+wb0)/Lspec)*(wb0-X)}
.FUNC  QTOT(X,Y)	{q*A_j/2/pi*(N1(X,Y)-N2(X)+N3(X)+NA1*(Lspec*log((Lspec**2+wb0**2)/Lspec**2)-2*wb0*arctan(wb0/Lspec)))}
.FUNC  QCOX_D(X,Y)	{q*Ads/2/pi*(N1(X,Y)-N2(X)+N3(X)+NA1*(Lspec*log((Lspec**2+wb0**2)/Lspec**2)-2*wb0*arctan(wb0/Lspec)))}
.FUNC  QCOX_FP(X,Y)	{q*(A-Ads)/2/pi*(N1(X,Y)-N2(X)+N3(X)+NA1*(Lspec*log((Lspec**2+wb0**2)/Lspec**2)-2*wb0*arctan(wb0/Lspec)))}
.FUNC	TAU_X(T,V)	{(TX2*((T+t0)/300)**{ETAUB})/(1+1*NA1/NA3*(0.5+1/pi*arctan((V-wb0)/Lspec)))}
.FUNC   VXX(T)		{psi2(T) + TX1*sqrt(2*eps0*epsi*q*NA2*psi2(T))/eps0/eox}
.FUNC   EG(T)		{1.16 - 7.02e-4*(T+t0)**2/(T+t0 + 1108)}
.FUNC   ni(T)		{ni0*((T+t0)/300)**1.5*exp(1/(2*8.61e-5)*(-300*EG(T)+(t0+T)*EG(300))/(T+t0)/300)}
.FUNC	psi2(T)		{2*k/q*(T + t0)*log(NA2/ni(T))}
.FUNC	DVt(T)		{psi2(T) - psi2(27) + TX1*(sqrt(2*eps0*epsi*q*NA2*psi2(T)) 
+ - sqrt(2*eps0*epsi*q*NA2*psi2(27)))/eps0/eox}
.FUNC   VTX(T)		{psi2(T) + TX1*sqrt(2*eps0*epsi*q*NA2*psi2(T))/eps0/eox}
.FUNC	DA(T)		{2*k*un*up/q/(un + up)*(T + t0)*(((T + t0)/300)**EMU)}
.FUNC	LA(T,V)		{sqrt(DA(T) * TAU_X(T,V)) }
.FUNC   Isx2(T)		{LIMIT(Isbs2_g*k*(t0+T)*(up + un)*(((T + t0)/300)**EMU)*ni(T)*ni(T),-1e6,1e6)} 
.FUNC   Isx1(T,V)	{LIMIT(Isbs1_g * sqrt(DA(T)/(TAU_X(T,V)))*ni(T),-1e6,1e6)} 
.FUNC   Vdep_fp(V,X,Y)	{V - 1/Cox_fp*QCOX_FP(X,Y)}
.FUNC   Vdep_d(V,X,Y)	{V - 1/Cox_d*QCOX_D(X,Y)}
.FUNC	ATX1(X)		{(MAX(X,.1m)**2 - wb0**2 +Lspec**2)*arctan((-MAX(X,.1m)+wb0)/Lspec)
+ - (Lspec**2 - wb0**2)*arctan(wb0/Lspec)}
.FUNC	ATX2(X)		{Lspec*MAX(X,.1m) + Lspec*wb0*log((Lspec**2 +(MAX(X,.1m) -wb0)**2)/(Lspec**2 +wb0**2))}
.FUNC	xj(V,Y2,X) {0.5*(MAX(X,.1m) + (2*eps0*epsi*MAX(V,0)/q/MAX(X,.1m)+NA1/pi/MAX(X,.1m)*(ATX1(X) + ATX2(X)) )/
+ (NA3 + MAX(Y2,0)/q/A/vlimit + NA1/2))}
.FUNC	Rb(X,T)		{(wb)*(wb)/(un*(Q0)*(((T + t0)/300)**EMU) + (un + up)*MAX(X,0)*1e-6*(((T + t0)/300)**EMU))}
.FUNC	FDX(V,X,T)	{LIMIT(TANH(MAX(wb-V,.1m)*1e6*MAX(X,0)/(8*DA(T))),-1e6,1e6)}
.FUNC	Td(V,X,T)	{LIMIT((0.1/DA(T))*MAX(wb-V,.1m)*MAX(wb-V,.1m)*(1-FDX(V,X,T)),-1e6,1e6)}
.FUNC	F1(V,T)		{LIMIT(TAU_X(T,V)*(COSH(MAX(wb-V,.1m)/LA(T,V))-1),-1e6,1e6)}
.FUNC	F2(V,X,T)	{LIMIT(0.5*(1+2*FDX(V,X,T)/(1+FDX(V,X,T)*FDX(V,X,T))),-1e6,1e6)}
.FUNC	F3(V,X,T)	{LIMIT(1+Td(V,X,T)/(TAU_X(T,V)),-1e6,1e6)}
.FUNC	QS0(V,T)	{LIMIT(q*A*LA(T,V)*ni(T)*TANH(0.5*MAX(wb-V,.1m)/LA(T,V)),-1e6,1e6)}
.FUNC	Qbd(Y,Z,V,X,T)	{LIMIT(MAX(Z,0)*1e-6+Td(V,X,T)*Y,-1e6,1e6)}
.FUNC	Qb0(Y,Z,V,X,T)	{LIMIT(2*Qbd(Y,Z,V,X,T)/(F3(V,X,T)+SQRT(F3(V,X,T)*F3(V,X,T) 
+ + Td(V,X,T)*3*IQR(V,T)*Qbd(Y,Z,V,X,T)/QS0(V,T))),-1e6,1e6)}
.FUNC	INE(Y,Z,V,X,T)	{LIMIT(0.75*IQR(V,T)*Qb0(Y,Z,V,X,T)**2/QS0(V,T),-1e6,1e6)}
.FUNC	IPC(Y,Z,V,X,T)	{LIMIT((1/3)*Y+(4/3)*(Qb0(Y,Z,V,X,T)/F1(V,T)
+ +MAX(Qb0(Y,Z,V,X,T)/TAU_X(T,V)+INE(Y,Z,V,X,T)-Y,0)*F2(V,X,T)),-1e6,1e6)}
.FUNC   IQR(V,T)	{Isx2(T)/QS0(V,T)}
.ENDS
*$
.SUBCKT	L4XXXM_L2 anode	kath PARAMS: TJ= 27 A = 1 N_ideal = 1
.PARAM
+q = 1.602e-19		eps0 = 8.85e-14	epsi = 11.8
+kb = 1.38e-23		T0 = 273	ni0 = 1.45e10
+un = 1350		up = 450
.PARAM  tau = 1u	
.PARAM  Nd = 1.2e14	wb0 = 55e-4
.PARAM  pi = 3.1416  Nbuf=1e15	  ETAUB = 2
.PARAM  wb = 75e-4 Lspec = 395.93u
.PARAM	BV = 650	Rc = .6m
.PARAM	vlimit =1.25e7
.PARAM  Ut =	23.517m
.PARAM  D =	15.874
.PARAM  L =	2.8173m
.PARAM	Ise0 =	{A*428p}
.PARAM	Ise_g  = {A*0.4p}
.PARAM  Ism0 =	{A*18.52u}
.PARAM	Ism_g = {A*3.204e-19}
.PARAM  QN =	{A*141n}
.PARAM	Ra =	{55.5u/A} 
RSERIE	anode	ano	{Ra}
GDE	ano	mi	VALUE = 
+ {LIMIT(TANH(1e3*I(VDE))*(((ISE(TJ)**((t0 + TJ)/(t0 + 27)))/ISE(27))**((t0 + 27)/(t0 + TJ)) 
+ * ABS(I(VDE))**((t0 + 27)/(t0 + TJ)) - ABS(I(VDE))),-1e6,1e6)}
GD0	ano	mi1	VALUE = 
+ {LIMIT(TANH(1e3*I(VD0))*(((ISM(TJ,V(xj1,0))**((t0 + TJ)/(t0 + 27)))/ISM(27,V(xj1,0)))**((t0 + 27)/(t0 + TJ)) 
+ * ABS(I(VD0))**((t0 + 27)/(t0 + TJ)) - ABS(I(VD0))),-1e6,1e6)}
VDE	ano	ano1	0
DE	ano1	mi	D1
D0	ano2	mi1	D2
VD0	ano	ano2	0
.model	D1	D	(is={Ise0}, BV = {BV}, N= {N_ideal})
.model  D2	D	(is={Ism0}, N=2)
VID0	mi1	mi	0
VITOT	kat	kath	0
GIR	mi	kat	VALUE = {V(mi,kat)/Rd(V(q,0),TJ)}
GID	ano	mi	VALUE = {IQ(V(xj1,0),I(VID0),V(q,0),I(VDXJ),TJ) - 1e6*I(VIJCT)}
CQB	q	0	1u
RHELP	q	0	1meg
GRQB	q	0	VALUE = {1u*V(q,0)/TAU_X(TJ,V(xj1,0)) }
GIQ	0	q	VALUE = {I(VID0) + IQ(V(xj1,0),I(VID0),V(q,0),I(VDXJ),TJ)}
EXJ	xj1 0	VALUE	{MAX(xj(V(mi,ano),I(VITOT),MAX(V(xj1,0),1e-4)),1e-4)}
CXJ	xj  0	1u
RXJ	xjr  xj 0.1m
VDXJ	xj1 xjr	0
EJUNCT	jct 0	VALUE = {QTOT(V(xj1,0))}
VIJCT	jct cpr	0
CJCT	cp   0	1u
RCJCT	cpr  cp  0.1m
.FUNC  LA(T,V)		{sqrt(DA(T) * TAU_X(T,V)) }
.FUNC  a0(T,V)		{-0.5*tanh(0.5*wb0/LA(T,V))}
.FUNC  a1(T,V)		{(up/un)*(1 + a0(T,V))/(1 - a0(T,V))}
.FUNC  a2(T,V)		{0.1/((1 - 0.5*(1 - a1(T,V)*a1(T,V))*(1 - a0(T,V))) )}
.FUNC  N1(X)		{(2*Nd + Nbuf)*pi*X}
.FUNC  N2(X)		{Nbuf*Lspec*log(1+((X-wb0)/Lspec)**2)}
.FUNC  N3(X)		{2*Nbuf*arctan((-X+wb0)/Lspec)*(wb0-X)}
.FUNC  QTOT(X)	{q*A/2/pi*(N1(X)-N2(X)+N3(X)+Nbuf*(Lspec*log((Lspec**2+wb0**2)/Lspec**2)-2*wb0*arctan(wb0/Lspec)))}
.FUNC	TAU_X(T,V)	{(Tau*((T+t0)/300)**{ETAUB})/(1+1*Nbuf/Nd*(0.5+1/pi*arctan((V-wb0)/Lspec)))}
.FUNC   EG(T)		{1.16 - 7.02e-4*(T + t0)**2/(T + t0 + 1108)}
.FUNC   ni(T)		{ni0*((T+t0)/300)**1.5*exp(1/(2*8.61e-5)*(-300*EG(T)+(t0+T)*EG(300))/(T+t0)/300)}
.FUNC	DA(T)		{2*kb*un*up/q/(un + up)*(T + t0)*(((T + t0)/300)**-1.5)}
.FUNC   ISE(T)  	{kb*(T + t0)*un*(((T + t0)/300)**-1.5)*ise_g*ni(T)*ni(T)}
.FUNC	ISM(T,V)	{Ism_g*ni(T)*sqrt(DA(T)/Tau_X(T,V))}
.FUNC	ATX1(X)		{(MAX(X,.1m)**2 - wb0**2 +Lspec**2)*arctan((-MAX(X,.1m)+wb0)/Lspec)
+ - (Lspec**2 - wb0**2)*arctan(wb0/Lspec)}
.FUNC	ATX2(X)		{Lspec*MAX(X,.1m) + Lspec*wb0*log((Lspec**2 +(MAX(X,.1m) -wb0)**2)/(Lspec**2 +wb0**2))}
.FUNC	xj(V,Y2,X) {0.5*(MAX(X,.1m) + (2*eps0*epsi*MAX(V,0)/q/MAX(X,.1m)+Nbuf/pi/MAX(X,.1m)*(ATX1(X) + ATX2(X)) )/
+ (Nd - MIN(Y2,0)/q/A/vlimit + Nbuf/2))}
.FUNC	w(V,T)		{MIN(MAX(wb-V*(1 + a1(T,V)),1e-4),wb-1e-4)}
.FUNC	Rd(Z,T)		{wb*wb/(un*QN*(((T + t0)/300)**-1.5) + (un + up)*MAX(Z,1m*QN)*1e-6*(((T + t0)/300)**-1.5))}
.FUNC	Td(V,U,T) 	{a2(T,V)*w(V,T)*w(V,T)/DA(T)/(1 + w(V,T)*1e6*MAX(U,0)/4/DA(T))}
.FUNC	IQ(V,Y,Z,U,T)	{LIMIT((Tau_X(T,V)*MAX(Y,0) - MAX(Z,1m*QN)*1e-6)/Td(V,U,T),-1e6,1e6)}
.ends
*$
