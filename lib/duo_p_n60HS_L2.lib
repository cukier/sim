*****************************************************************
* Level2 Version of 600V HS-IGBT/EMCON HEDIODE Models feb 2002	*
*****************************************************************
* INFINEON TECHNOLOGIES AG      	duo_p_n60HS_L2.LIB	*
*								*
*  (V1.0a)     	02/02  						*
*                                                               *
* Models provided by INFINEON are not warranted by INFINEON as  *
* fully representing all of the specifications and operating    *
* characteristics of the semiconductor product to which the     *
* model relates. The model describe the characteristics of a    * 
* typical device.                                               * 
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. INFINEON therefore does not  *
* assume any liability arising from their use.                  *
* INFINEON reserves the right to change models without prior    * 
* notice.                                                       *
*                                                               *
* This library contains Level 2 models for the following	* 
* INFINEON Technologies 600 V HS_IGBT/EMCON HE Power Diodes	*
* devices/chip-models including lead inductances related	*
*								*
* Model for 600 V HS-IGBT/EMCON-FAST DuoPacks			*	
* devices/chip-models including lead inductances related to	* 
* packages:							*
* SKW20N60HS							*
* SKW30N60HS							*
* 								*
* the model is based on the publication:			*
* 								*
* R.Kraus, P.Türkes, J.Sigg					*
* Physics-based Models Of Power Semiconductor Devices 		*
* For The Circuit Simulator Spice				*
* 								*
* Power Electronics Specialists Conference, 1998.		* 
* PESC 98 Record. 29th Annual IEEE				*
*								*
* SUPPORTemail:	simulate@infineon.com				*
*****************************************************************
.SUBCKT SKW20N60HS_L2 ano gate kat PARAMS: TJ = 27 
LANO	ano	anol		5n
RLAN	ano	anol		1
LKAT	katl	kat		7n	
RLK	katl	kat		1
XIGBT	anol	gate	katl	L7xxxU_L2	PARAMS: TJ = {TJ} A_total = 0.185	A_aktiv=0.142
XDIO	katl	anol		L4XXX-M		PARAMS: TJ = {TJ} A=0.0512	N_ideal = 1.05
.ENDS
.SUBCKT SKW30N60HS_L2 ano gate kat PARAMS: TJ = 27 
LANO	ano	anol		5n
RLAN	ano	anol		1
LKAT	katl	kat		7n	
RLK	katl	kat		1
XIGBT	anol	gate	katl	L7xxxU_L2	PARAMS: TJ = {TJ} A_total = 0.257	A_aktiv=0.207
XDIO	katl	anol		L4XXX-M		PARAMS: TJ = {TJ} A=0.067	N_ideal = 1.05
.ENDS
.SUBCKT	L4XXX-M anode	kath PARAMS: TJ= 27 A = 1 N_ideal = 1
* physical parameters: Nd = 1.2e14
.PARAM
+q = 1.602e-19		eps0 = 8.85e-14	epsi = 11.8
+kb = 1.38e-23		T0 = 273	ni0 = 1.45e10
+un = 1350		up = 450
.PARAM  tau = 0.5u	
.PARAM  Nd = 1.2e14	w0 = 70e-4
.PARAM	SF = -1	BV = 800	Rc = .6m
.PARAM	vlimit =1.5e7
.PARAM  Vdiff=  0.396m
.PARAM  Ut =	23.5m
.PARAM  D =	15.87
.PARAM  L =	2.817m
.PARAM	Ise0 =	{A*356.2p}
.PARAM	Ise_g  = {A*0.333p}
.PARAM  Ism0 =	{A*26.173u}
.PARAM	Ism_g = {A*2*q}
.PARAM  CJ0 =	{A*3.787n}
.PARAM  QN =	{A*134.57n}
.PARAM  XF =	3610.7
.PARAM	VPT=	450
.PARAM  a0 =	{SF*tanh(1.2425)}
.PARAM  a1 =	{(up/un)*(1 + a0)/(1 - a0)}
.PARAM  a2 =	{0.1/((1 - 0.5*(1 - a1*a1)*(1 - a0)) )}
.PARAM  Rd0 =	{269.72m/A} 
.PARAM	Ra =	{739.82u/A} 
RSERIE	anode	ano	{Ra}
GDE	ano	mi	VALUE = 
+ {LIMIT(TANH(1e3*I(VDE))*(((ISE(TJ)**((t0 + TJ)/(t0 + 27)))/ISE(27))**((t0 + 27)/(t0 + TJ)) 
+ * ABS(I(VDE))**((t0 + 27)/(t0 + TJ)) - ABS(I(VDE))),-1e6,1e6)}
GD0	ano	mi1	VALUE = 
+ {LIMIT(TANH(1e3*I(VD0))*(((ISM(TJ)**((t0 + TJ)/(t0 + 27)))/ISM(27))**((t0 + 27)/(t0 + TJ))
+ * ABS(I(VD0))**((t0 + 27)/(t0 + TJ)) - ABS(I(VD0))),-1e6,1e6)}
VDE	ano	ano1	0
DE	ano1	mi	D1
D0	ano2	mi1	D2
VD0	ano	ano2	0
.model	D1	D	(is={Ise0}, N= {N_ideal})
.model  D2	D	(is={Ism0}, N=2)
VID0	mi1	mi	0
VITOT	kat	kathv	0
EVDIFF	kathv	kath	VALUE = {(TJ + t0)* Vdiff}
EGIR	mi	kat	VALUE = {I(VITOT)*Rd(V(q,0),TJ)}
GID	ano	mi	VALUE = {IQ(V(xj,0),I(VID0),V(q,0),I(VDXJ),TJ) - I(VIJCT)}
RGID	mi	0	100meg
CQB	q	0	1u
RHELP	q	0	1meg
GRQB	q	0	VALUE = {1u*V(q,0)/(Tau*((TJ + t0)/300)**2) }
GIQ	0	q	VALUE = {MAX(I(VID0),0) + IQ(V(xj,0),I(VID0),V(q,0),I(VDXJ),TJ)}
EXJ	xj1 0	VALUE	{MAX(xj(V(ano,mi),I(VITOT),V(xj1,0)),1e-4) }
CXJ	xj  0	1n
VDXJ	xj1 xj	0
EJUNCT	jct 0	VALUE = {LIMIT(V(kat,ano),0,VPT)}
VIJCT	jct cp	0
DJCT	0   cp	DXXX
.MODEL  DXXX	D (is = 1p, CJO= {CJ0})
.FUNC   EG(T)			{1.16 - 7.02e-4*(T + t0)**2/(T + t0 + 1108)}
.FUNC	DA(T)			{2*kb*un*up/q/(un + up)*(T + t0)*(((T + t0)/300)**-1.5)}
.FUNC   ISE(T)  		{(T + t0)*un*(((T + t0)/300)**-1.5)*((ise_g*(ni0*((T + t0)/300)**1.5*exp(1/(2*8.61e-5)
+ *(-300*EG(T)+(t0 + T)*EG(27))/(T + t0)/300)))
+ *(kb*(ni0*((T + t0)/300)**1.5*exp(1/(2*8.61e-5)*(-300*EG(T)+(t0 + T)*EG(27))/(T + t0)/300))))}
.FUNC	ISM(T)			{Ism_g*(ni0*((T + t0)/300)**1.5*exp(1/(2*8.61e-5)*(-300*EG(T)+(t0 + T)*EG(27))/(T + t0)/300))
+ *sqrt(DA(T)/(Tau*((T + t0)/300)**2))}
.FUNC	xj(V,Y2,X) {0.5*((XF*XF*((1-limit(V,-VPT,0)))/(Nd+MAX(up*(-Y2)/(un+up),0)/A/q/(vlimit)))/MAX(X,.1m) + MAX(X,.1m))}
.FUNC	w(V)			{MAX((w0-V)*(1 + a1),1e-4)}
.FUNC	Rd(Z,T)			{w0*w0/(un*QN*(((T + t0)/300)**-1.5) + (un + up)*MAX(Z,1m*QN)*1e-6*(((T + t0)/300)**-1.5))}
.FUNC	Td(V,U,T) 		{a2*w(V)*w(V)/DA(T)/(1 + w(V)*1e9*LIMIT(U,0,1)/4/DA(T))}
.FUNC	IQ(V,Y,Z,U,T)		{((Tau*((T + t0)/300)**2)*Y - MAX(Z,1m*QN)*1e-6)/(Td(V,U,T))}
* end of functions
.ends
.SUBCKT L7xxxU_L2 ano gate kat PARAMS: TJ = 27 A_total = 0.1	A_aktiv=0.1
.PARAM 
+k=1.380622e-23		q = 1.6E-19	t0=273		ni0=1.45e10
+eps0 = 8.85E-14	epsi =11.8	eox= 2.8
+un = 1350		up = 450	u_surf=600
+a1 = 0.55		vlimit= 1.5e7	vsw= -0.5
.PARAM tox = 80e-7	ttox = 1.5e-4 wb = 90E-4 lchann = 2.5e-4
.PARAM n_cells = {A_aktiv*118.91k}
.PARAM wchann = {n_cells*5.2m}
.PARAM Agd  = {n_cells*6.72u}
.PARAM Cox  = {Agd*30.975n}
.PARAM Cgs  = {n_cells*4.536e-14}
.PARAM Nb = 15E13	taub = 125u Nsource= 1.5e17  BV_FW=800	BV_RW=280 
.PARAM Rs = 2.5m	 Rg = 0.5
.PARAM Cjs  = {A_aktiv*4.2338n}
.PARAM Cje  = {(A_total/A_aktiv)*Cjs}	
.PARAM Ise_x = {A_total*940.1p}
.PARAM Ise_g  = {A_total*0.8p}
.PARAM Ise_k  = {A_total*15.668p}
.PARAM	EG0 = {1.16 - (7.02e-4*(t0+27)**2/((t0+27)+1108))}
.PARAM	ni_fak = {ni0*exp(EG0*q/(2*k*(t0+27)))}
.PARAM	D  = 17.55
.PARAM	L  = 46.837m	
.PARAM 	Q0 = {A_aktiv*216.27n}
.PARAM	VPT= 930
.PARAM	VN = {2.51e-17*Agd*Agd/Cox/Cox}
.PARAM	XF = 294.82u
.PARAM islimit = 0.3f
MFET	d g s s 	MOS W={wchann/100},L={lchann/100} , IC=OFF
GMFET_T d1x	s	VALUE = {LIMIT(I(VINC0)*((TJ+t0)/300)**-1.5,0,1e6) }
GDE	ano	e1	VALUE = 
+ {LIMIT(TANH(1e3*I(VDE))*(((ISE2(TJ)**((t0 + TJ)/(t0 + 27)))/ISE2(27))**((t0 + 27)/(t0 + TJ)) 
+ * ABS(I(VDE))**((t0 + 27)/(t0 + TJ)) - ABS(I(VDE))),-1e6,1e6)}
VDE	ano	anx	0
DE	anx	e1	D1
DS	kat	d1	D2
.MODEL  MOS     NMOS    (LEVEL=3,nsub={Nsource},tox={tox/100},vmax={vlimit},uo={u_surf},CGSO=1p,CGDO=1p)
.MODEL 	D1	D	(IS={Ise_x},N =1.1)
.MODEL	D2	D	(IS={Ise_k},CJO={CJS},BV= {2*BV_FW})
ETHERM  g_t	g	VALUE = {DVt(TJ)}
RG	gate	g_t	{Rg*(((TJ + t0)/300)**1.5)}
RS	s	kat	{Rs}
EVGCOX 	vgcox 0 VALUE { V(ox,kat)-{vsw} }
RVGC	vgcox 0 100meg
GICOX	g     ox	VALUE = {LIMIT((Cox/100  + 99*Cox*(1+tanh(10*(V(vgcox))))/200)* 1e9*I(VDUGD),-100,100)}
GICGS	g     s		VALUE = {LIMIT((Cgs + 99*Cox*(1+tanh(-10*(V(vgcox))))/200)*1e9* I(VDUGS),-100,100)}
EDEP	d1 ox	VALUE = {LIMIT(Vdep(V(d1,g),V(xj,0)),0,V(d1,g))}
RDEP	ox 0	100meg
EGIA	e d1	VALUE = {I(VIA)*Rb(V(b,0),TJ)}
GIC	d1  kat	VALUE = {LIMIT(IPC(I(VINC),V(b,0),V(xj,0),I(VDXJ),TJ),-1e6,1e6)}
RGIC	d1	0	100meg
CQB	b 0	1u
RHELP	b 0	1meg
GRQB	b 0	VALUE   {1u*V(b,0)/(Taub*((TJ + t0)/300)**2) }
GINC	0 b	VALUE	{MAX(I(VINC),0)}
GINE	b 0	VALUE	{INE(I(VINC),V(b,0),V(xj,0),I(VDXJ),TJ)}
VIA	e1 e	0
EAUX1	d12 s	VALUE = {V(d1,s)}
VINC0	d12 d	0
VINC	d1 d1x	0
EXJ	xj1 0	VALUE	{MAX(xj(V(d1,kat),V(xj1,0)),1e-4)}
CXJ	xj  0	1n
VDXJ	xj1 xj	0
EDUGS	du1	0	VALUE {V(g,s)}
VDUGS	du1	du1c	0	
CDUGS	du1c	0	1n
EDUGD	du2	0	VALUE {V(g,ox)}
VDUGD	du2	du2c	0	
CDUGD	du2c	0	1n
.FUNC   EG(T)		{1.16 - 7.02e-4*(T+t0)**2/(T+t0 + 1108)}
.FUNC   ni(T)		{1e-10*ni0*((T+t0)/300)**1.5*exp(1/(2*8.61e-5)*(-300*EG(T)+(t0+T)*EG(300))/(T+t0)/300)}
.FUNC	psi2(T)		{2*k/q*(T + t0)*(log(Nsource/ni(T)) - log(1e10))}
.FUNC	DVt(T)		{psi2(T) - psi2(27) + tox*(sqrt(2*eps0*epsi*q*Nsource*psi2(T)) 
+ - sqrt(2*eps0*epsi*q*Nsource*psi2(27)))/eps0/eox}
.FUNC	DA(T)		{2*k*un*up/q/(un + up)*(T + t0)*(((T + t0)/300)**-1.5)}
.FUNC	LA(T)		{sqrt(DA(T) * Taub*((T+t0)/300)**2) }
.FUNC   ISE2(T)		{LIMIT(1e20*ise_g*k*(T +t0)*(un*((T+t0)/300)**-1.5)*ni(T)*ni(T),0,1e8)}
.FUNC   Vdep(V,X)	{LIMIT(V - Agd/Cox * (q*Nb)* X,0,V)}
.FUNC	Rb(X,T)		{wb*wb/(un*Q0*(((T + t0)/300)**-1.5) + (un + up)*MAX(X,1m*Q0)*1e-6*(((T + t0)/300)**-1.5))}
.FUNC	xj(V,X)		{LIMIT(0.5*(XF*XF*(1+LIMIT(V,0,VPT))/MAX(X,.1m) +MAX(X,.1m)),.1m,wb-1e-4)}
.FUNC	FDX(V,X,T)	{TANH(MAX(wb-V,.1m)*1e9*LIMIT(X,0,1)/(24*DA(T)))}
.FUNC	Td(V,X,T)	{LIMIT((0.1/DA(T))*MAX(wb-V,.1m)*MAX(wb-V,.1m)*(1-FDX(V,X,T)),-1e6,1e6)}
.FUNC	F1(V,T)		{LIMIT(Taub*((T+t0)/300)**2*(COSH(MAX(wb-V,.1m)/LA(T))-1),-1e6,1e6)}
.FUNC	F2(V,X,T)	{LIMIT(0.5*(1+2*FDX(V,X,T)/(1+FDX(V,X,T)*FDX(V,X,T))),-1e6,1e6)}
.FUNC	F3(V,X,T)	{LIMIT(1+Td(V,X,T)/(Taub*((T+t0)/300)**2),-1e6,1e6)}
.FUNC	QS0(V,T)	{LIMIT(q*A_aktiv*LA(T)*1e10*ni(T)*TANH(0.5*MAX(wb-V,.1m)/LA(T)),0,1e6)}
.FUNC	Qbd(Y,Z,V,X,T)	{LIMIT(MAX(Z,1m*Q0)*1e-6+Td(V,X,T)*Y,0,1e6)}
.FUNC	Qb0(Y,Z,V,X,T)	{LIMIT(2*Qbd(Y,Z,V,X,T)/(F3(V,X,T)+SQRT(F3(V,X,T)*F3(V,X,T) 
+ + Td(V,X,T)*3*MAX(ISE2(T),islimit)*Qbd(Y,Z,V,X,T)/QS0(V,T)/QS0(V,T))),0,1e6)}
.FUNC	INE(Y,Z,V,X,T)	{LIMIT(0.75*MAX(ISE2(T),islimit)*(Qb0(Y,Z,V,X,T)/QS0(V,T))*(Qb0(Y,Z,V,X,T)/QS0(V,T)),0,1e6)}
.FUNC	IPC(Y,Z,V,X,T)	{LIMIT((1/3)*Y+(4/3)*(Qb0(Y,Z,V,X,T)/F1(V,T)
+ +MAX(Qb0(Y,Z,V,X,T)/Taub/((T+t0)/300)**2+INE(Y,Z,V,X,T)-Y,0)*F2(V,X,T)),0,1e6)}
.ENDS